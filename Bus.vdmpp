class Bus

instance variables
  public id: seq of char := "TEMP";
  public placa: seq of char := "TEMP";
  public empresa: seq of char := "";
  public capacidad: nat1 := 10;
  public rutaAsignada: [Ruta] := nil;
  public ubicacionActual: [Coordenada] := nil;
  public conductorActual: [Conductor] := nil;
  public enServicio: bool := false;
  public pasajerosActuales: nat := 0;

operations
  public Bus: seq of char * seq of char * seq of char * nat1 * Ruta * Coordenada ==> Bus
  Bus(id_bus, p, emp, cap, ruta, ubic) == (
    id := id_bus;
    placa := p;
    empresa := emp;
    capacidad := cap;
    rutaAsignada := ruta;
    ubicacionActual := ubic;
    conductorActual := nil;
    enServicio := false;
    pasajerosActuales := 0;
    return self;
  )
  pre len id_bus > 0 and len p > 0 and cap >= 10 and cap <= 90
  post id = id_bus and placa = p and empresa = emp and capacidad = cap and 
       rutaAsignada = ruta and ubicacionActual = ubic and 
       conductorActual = nil and enServicio = false and 
       pasajerosActuales = 0;

  public actualizarUbicacion: Coordenada ==> ()
  actualizarUbicacion(nuevaUbic) == (
    ubicacionActual := nuevaUbic;
  )
  pre nuevaUbic <> nil
  post ubicacionActual = nuevaUbic;

  public asignarConductor: Conductor ==> bool
  asignarConductor(conductor) == (
    if conductor.activo then (
      conductorActual := conductor;
      return true;
    )
    else return false;
  )
  pre conductor <> nil
  post (RESULT = true => conductorActual = conductor) and
       (RESULT = false => conductorActual = conductorActual~);

  public cambiarEstadoServicio: bool ==> ()
  cambiarEstadoServicio(estado) == (
    enServicio := estado;
    if estado = false then conductorActual := nil;
  )
  pre estado = true => conductorActual <> nil
  post enServicio = estado and
       (estado = false => conductorActual = nil);

  public subirPasajeros: nat ==> bool
  subirPasajeros(cantidad) == (
    if pasajerosActuales + cantidad <= capacidad then (
      pasajerosActuales := pasajerosActuales + cantidad;
      return true;
    )
    else return false;
  )
  pre cantidad > 0 and enServicio = true
  post (RESULT = true => pasajerosActuales = pasajerosActuales~ + cantidad) and
       (RESULT = false => pasajerosActuales = pasajerosActuales~);

  public bajarPasajeros: nat ==> bool
  bajarPasajeros(cantidad) == (
    if cantidad <= pasajerosActuales then (
      pasajerosActuales := pasajerosActuales - cantidad;
      return true;
    )
    else return false;
  )
  pre cantidad > 0 and enServicio = true
  post (RESULT = true => pasajerosActuales = pasajerosActuales~ - cantidad) and
       (RESULT = false => pasajerosActuales = pasajerosActuales~);

  public calcularOcupacion: () ==> real
  calcularOcupacion() == (
    return (pasajerosActuales * 100.0) / capacidad;
  )
  post RESULT >= 0 and RESULT <= 100;

  public estimarTiempoArribo: Paradero * real * real ==> real
  estimarTiempoArribo(paraderoDestino, factorCongestion, velocidadPromedio) == (
    dcl distancia: real := ubicacionActual.calcularDistancia(
                           paraderoDestino.obtenerUbicacion());
    dcl velocidadMS: real := velocidadPromedio * 1000.0 / 3600.0;
    dcl tiempoBase: real := distancia / velocidadMS;
    dcl tiempoAjustado: real := tiempoBase * factorCongestion;
    dcl tiempoMinutos: real := tiempoAjustado / 60.0;
    
    return tiempoMinutos;
  )
  pre enServicio = true and paraderoDestino <> nil and
       factorCongestion >= 0.5 and factorCongestion <= 3.0 and
       velocidadPromedio > 0
  post RESULT >= 0;

  public validarParadaEnParadero: Coordenada * real ==> bool
  validarParadaEnParadero(ubicacionParada, radioTolerancia) == (
    dcl paraderos: seq of Paradero := rutaAsignada.obtenerParaderos();
    for i = 1 to len paraderos do (
      if paraderos(i).estaEnRadio(ubicacionParada) and 
         ubicacionActual.calcularDistancia(ubicacionParada) <= radioTolerancia then
        return true;
    );
    return false;
  )
  pre ubicacionParada <> nil and radioTolerancia > 0;

  public obtenerInfoBus: () ==> map seq of char to seq of char | nat | real | bool
  obtenerInfoBus() == (
    return {
      "id"            |-> id,
      "placa"         |-> placa, 
      "empresa"       |-> empresa,
      "capacidad"     |-> capacidad,
      "pasajeros"     |-> pasajerosActuales,
      "ocupacion"     |-> calcularOcupacion(),
      "en_servicio"   |-> enServicio,
      "ruta"          |-> rutaAsignada.obtenerCodigo()
    };
  );

end Bus