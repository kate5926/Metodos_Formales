class Ruta

instance variables
  public codigo: seq of char := "TEMP";
  public nombre: seq of char := "";
  public paraderos: seq of Paradero := [];
  public velocidadPromedio: real := 30.0;
  public factorCongestion: real := 1.0;
  public empresaOperadora: seq of char := "";
  public activa: bool := true;

operations
  public Ruta: seq of char * seq of char * seq of Paradero * real * seq of char ==> Ruta
  Ruta(cod, nom, parad, vel, empresa) == (
    codigo := cod;
    nombre := nom;
    paraderos := parad;
    velocidadPromedio := vel;
    factorCongestion := 1.0;
    empresaOperadora := empresa;
    activa := true;
    return self;
  )
  pre len cod > 0 and len parad >= 2 and vel > 0 and vel <= 60
  post codigo = cod and nombre = nom and paraderos = parad and 
       velocidadPromedio = vel and factorCongestion = 1.0 and 
       empresaOperadora = empresa and activa = true;

  public obtenerCodigo: () ==> seq of char
  obtenerCodigo() == (
    return codigo;
  )
  post RESULT = codigo;

  public obtenerParaderos: () ==> seq of Paradero
  obtenerParaderos() == (
    return paraderos;
  )
  post RESULT = paraderos;

  public actualizarCongestion: real ==> ()
  actualizarCongestion(factor) == (
    factorCongestion := factor;
  )
  pre factor >= 1.0 and factor <= 2.0
  post factorCongestion = factor;

  public desactivar: () ==> ()
  desactivar() == (
    activa := false;
  )
  post activa = false;

  public activar: () ==> ()
  activar() == (
    activa := true;
  )
  post activa = true;

  public distanciaTotal: () ==> real
  distanciaTotal() == (
    dcl total: real := 0.0;
    for i = 1 to len paraderos - 1 do (
      total := total + paraderos(i).ubicacion.calcularDistancia(
                       paraderos(i+1).ubicacion);
    );
    return total;
  )
  post RESULT >= 0;

  public paraderoMasCercano: Coordenada ==> nat1
  paraderoMasCercano(coord) == (
    dcl minDist: real := paraderos(1).ubicacion.calcularDistancia(coord);
    dcl indiceMin: nat1 := 1;
    for i = 2 to len paraderos do (
      dcl dist: real := paraderos(i).ubicacion.calcularDistancia(coord);
      if dist < minDist then (
        minDist := dist;
        indiceMin := i;
      );
    );
    return indiceMin;
  )
  pre len paraderos > 0
  post RESULT >= 1 and RESULT <= len paraderos;

  public contarParaderosOficiales: () ==> nat
  contarParaderosOficiales() == (
    dcl count: nat := 0;
    for i = 1 to len paraderos do (
      if paraderos(i).esOficial then
        count := count + 1;
    );
    return count;
  )
  post RESULT >= 0 and RESULT <= len paraderos;

end Ruta